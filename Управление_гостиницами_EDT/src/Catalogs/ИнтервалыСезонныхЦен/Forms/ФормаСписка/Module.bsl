
&НаКлиенте
Процедура УстановитьЗаголовокКнопкиВыбораПериода()

	Элементы.кнВыборПериода.Заголовок = Формат(ТекущийПериод,"ЧГ=") + " г."	

КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПериода(Результат,ДопПараметры) Экспорт
	Если НЕ Результат = Неопределено Тогда
		ТекущийПериод = Результат;
		Список.Параметры.УстановитьЗначениеПараметра("Период",ТекущийПериод);
		УстановитьЗаголовокКнопкиВыбораПериода();
			
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ВыборПериода(Команда)
	ПараметрыФормы = Новый Структура("ТекущийПериод",ТекущийПериод);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораПериода",ЭтаФорма);
	ОткрытьФорму("Обработка.ПериодыБронирования.Форма",ПараметрыФормы,ЭтаФорма,ЭтаФорма,,,ОписаниеОповещения,РежимОткрытияОкнаФОрмы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаСервере
Процедура УстановитьВладельца()

	Если Параметры.Свойство("Отбор") Тогда
		Если Параметры.Отбор.Свойство("Владелец") Тогда
			Владелец = Параметры.Отбор.Владелец;
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ТекущийПериод = 0 Тогда
		ТекущийПериод = ГОД(ТекущаяДатаСеанса());
	КонецЕсли;
	УстановитьВладельца();
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьЗаголовокКнопкиВыбораПериода();       
	Список.Параметры.УстановитьЗначениеПараметра("Период",ТекущийПериод);
	Если Параметры.РежимВыбора Тогда
		Элементы.кнВыборПериода.Доступность = Ложь;	
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	ЗначенияЗаполнения = Новый Структура("Период,Владелец",ТекущийПериод,Владелец);
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения",ЗначенияЗаполнения);
	ОткрытьФорму("Справочник.ИнтервалыСезонныхЦен.ФормаОбъекта",ПараметрыФормы,ЭтаФорма,ЭтаФорма);

КонецПроцедуры


&НаСервере
Процедура СкопироватьПредыдущийПериодСервер()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИнтервалыСезонныхЦен.ВидИнтервала КАК ВидИнтервала,
		|	ИнтервалыСезонныхЦен.ДатаНачалаДействияЦены КАК ДатаНачалаДействияЦены
		|ПОМЕСТИТЬ ИнтервалыТекущегоПериода
		|ИЗ
		|	Справочник.ИнтервалыСезонныхЦен КАК ИнтервалыСезонныхЦен
		|ГДЕ
		|	ИнтервалыСезонныхЦен.Период = &Период
		|	И ИнтервалыСезонныхЦен.Владелец = &Владелец
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИнтервалыСезонныхЦен.ВидИнтервала КАК ВидИнтервала,
		|	ИнтервалыСезонныхЦен.ДатаНачалаДействияЦены КАК ДатаНачалаДействияЦены
		|ИЗ
		|	Справочник.ИнтервалыСезонныхЦен КАК ИнтервалыСезонныхЦен
		|ГДЕ
		|	ИнтервалыСезонныхЦен.Период В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
		|				ИнтервалыСезонныхЦен.Период КАК Период
		|			ИЗ
		|				Справочник.ИнтервалыСезонныхЦен КАК ИнтервалыСезонныхЦен
		|			ГДЕ
		|				НЕ ИнтервалыСезонныхЦен.Период = &Период
		|			УПОРЯДОЧИТЬ ПО
		|				Период УБЫВ)
		|	И ИнтервалыСезонныхЦен.ПометкаУдаления = ЛОЖЬ
		|	И ИнтервалыСезонныхЦен.Владелец = &Владелец
		|	И НЕ ИнтервалыСезонныхЦен.ВидИнтервала В
		|				(ВЫБРАТЬ
		|					ИнтервалыТекущегоПериода.ВидИнтервала КАК ВидИнтервала
		|				ИЗ
		|					ИнтервалыТекущегоПериода КАК ИнтервалыТекущегоПериода)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаНачалаДействияЦены";
	
	Запрос.УстановитьПараметр("Период", ТекущийПериод);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяЗаписьИнтервала = Справочники.ИнтервалыСезонныхЦен.СоздатьЭлемент();
		НоваяЗаписьИнтервала.ВидИнтервала = Выборка.ВидИнтервала;
		НоваяЗаписьИнтервала.ДатаНачалаДействияЦены = Дата(ТекущийПериод,Месяц(Выборка.ДатаНачалаДействияЦены),День(Выборка.ДатаНачалаДействияЦены));
		НоваяЗаписьИнтервала.Период = ТекущийПериод;
		НоваяЗаписьИнтервала.Владелец = Владелец;
		НоваяЗаписьИнтервала.Записать();
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СкопироватьПредыдущийПериод(Команда)

	СкопироватьПредыдущийПериодСервер();
	Элементы.Список.Обновить();
КонецПроцедуры


&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	// Вставить содержимое обработчика.
КонецПроцедуры

